apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'
apply from: "$rootDir/gradle/jacoco-utils.gradle"
apply from: "$rootDir/gradle/checkstyle.gradle"

android {

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility androidConfig.javaVersion
        targetCompatibility androidConfig.javaVersion
    }

    compileSdkVersion androidConfig.compileSdkVersion
    buildToolsVersion androidConfig.buildToolsVersion

    defaultConfig {
        minSdkVersion androidConfig.minSdkVersion
        targetSdkVersion androidConfig.targetSdkVersion
        versionCode versionCode
        versionName versionNameLib

        testInstrumentationRunner androidConfig.testInstrumentationRunner

        buildConfigField "byte[]", "PREF_SALT", "new byte[]{0x4F, 0x00, 0x10, 0x2E, 0x62, 0x45, 0x4C, 0x0F, 0x3B, (byte) 0xF9, 0x37, 0x50, 0x47, 0x22, 0x24, 0x5D, (byte) 0xE0, (byte) 0xD8, 0x55, (byte) 0xC1, (byte) 0xBA, 0x23, (byte) 0x8D, (byte) 0xA9, (byte) 0xF0, (byte) 0xA3, 0x77, 0x4D, 0x38, 0x61, (byte) 0xF0, (byte) 0xBB};"
        buildConfigField "byte[]", "STATIC_RANDOM", "new byte[]{(byte) 0xBC, 0x1D, (byte) 0x91, 0x7B, (byte) 0xB3, (byte) 0xD7, (byte) 0xA4, 0x19, 0x2C, 0x72, 0x52, (byte) 0xF4, 0x34, (byte) 0xDC, (byte) 0xCC, 0x4F, (byte) 0xDC, (byte) 0xD8, (byte) 0xEF, 0x51, (byte) 0xF0, (byte) 0xA9, 0x5B, (byte) 0xDD, 0x62, 0x1A, 0x53, 0x10, (byte) 0xDD, 0x21, (byte) 0x83, (byte) 0xBD};"
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    api libDependencies.hkdf
    api libDependencies.bytesUtils
    api libDependencies.bcrypt
    api libDependencies.timber
    api libDependencies.annotation

    testImplementation testDependencies.junit
    testImplementation testDependencies.jBCrypt

    androidTestImplementation testDependencies.testRunner
    androidTestImplementation testDependencies.espresso
    androidTestImplementation testDependencies.jBCrypt
    androidTestImplementation testDependencies.bouncyCastle
}

apply from: "$rootDir/gradle/publish.gradle"
